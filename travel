

=== travel_menu ===

// Get the terrain if we haven't set the terrain yet.
{terrain == unknown_terrain:
    -> edit_terrain ->
    // If we still haven't set the terrain, return to previous menu.
    {terrain == unknown_terrain: ->-> }
}

// Go to the appropriate travel sub-menu.
{terrain:
    - dungeon:  -> dungeon_menu ->->
    - urban:  -> urban_menu ->->
    - else: -> wilderness_menu ->->
}



== base_travel_menu ===

+ [Change Terrain ({DisplayTerrain()})] -> edit_terrain ->
    ~ population = pop_unknown
    -> back_menu
+ {time == day}[Change Time to Night]
    ~ time = night
    -> back_menu
+ {time == night}[Change Time to Day]
    ~ time = day
    -> back_menu
+ [Change Population Density ({DisplayPopulation()})] -> edit_population -> back_menu
+ [Change Season ({DisplaySeason()})] -> edit_season -> back_menu



=== find_campsite_check ===

{terrain:
    - forest: ~ difficulty = 6
    - coastal: ~ difficulty = 8
    - desert: ~ difficulty = 8
    - grass: ~ difficulty = 8
    - hill: ~ difficulty = 6
    - dungeon: ~ difficulty = 10
    - swamp: ~ difficulty = 12
    - mountain: ~ difficulty = 14
    - else:
        ~logError("Unknown terrain '{terrain}'.")
        ~difficulty = 6
}


+ [Roll Survival ({DisplayBonus(stat_survival_bonus)}) vs. {DisplayTerrain()} (DC {difficulty})]

+ [Edit Survival ({DisplayBonus(stat_survival_bonus)})] -> edit_survival -> find_campsite_check
- 
~ roll = d20()
~ bonus = stat_survival_bonus
~ result = roll + bonus

{result >= difficulty || roll == 20:
    {DisplayD20Result(difficulty)}
    You find a campsite.
- else:
    {DisplayD20Result(difficulty)}
    No proper campsite found. Where did you have to sleep instead? You will be exhausted in the morning.
}

- ->->


=== disturbance_while_camping_check ===
-> yes_no_test("How likely are you to be disturbed? Adjust based on population and how much you aced the Survival check.", -> yes, -> no, -> yes)

= yes
You are disturbed by <>

~ roll = d100()
~ bonus = 0

{
    - roll <= 20: a loud noise. ({DisplayResult()})
    - roll <= 35: an animal (indifferent). ({DisplayResult()})
    - roll <= 50: an animal (hostile). ({DisplayResult()})
        // Go to the 'creatures' table.
        -> create_creature ->
    - roll <= 60: a disturbing dream/vision. ({DisplayResult()})
    - roll <= 80: an NPC (curious). ({DisplayResult()})
    - roll <= 85: a bandit/thief. ({DisplayResult()})
    - roll <= 90: a monster. ({DisplayResult()})
        // Roll for 'monster by terrain'.
        -> monster_by_terrain_table ->
    - roll == 91: a natural distaster. ({DisplayResult()})
    - else: storm/weather change. ({DisplayResult()})
}

- ->->

= no
No.
->->

= maybe
Maybe.
->->

