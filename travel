
=== camping_menu ===
<- menu
<- add_settings_menu(-> camping_menu)
<- add_back_menu(-> back)
-> DONE

- (back)
->->

= menu

{location == wilderness && terrain == (): -> edit_terrain ->}

<b> {DisplayLocation()} Camping Menu</b> ({DisplaySummary()})
    
+ Find Campsite Check
    -> find_campsite_check -> camping_menu

+ Disturbances While Camping Check
    -> disturbance_while_camping_check -> camping_menu

+ [Back]

- ->->


=== find_campsite_check ===
<- menu
<- add_settings_menu(-> find_campsite_check)
<- add_back_menu(-> back)
-> DONE

- (back)
->->

= menu

{location == dungeon:
    ~ difficulty = 10
- else:
    {terrain:
        - forest: ~ difficulty = 6
        - coastal: ~ difficulty = 8
        - desert: ~ difficulty = 8
        - grass: ~ difficulty = 8
        - hill: ~ difficulty = 6
        - swamp: ~ difficulty = 12
        - mountain: ~ difficulty = 14
        - else:
            ~logError("Unknown terrain '{terrain}'.")
            ~difficulty = 6
    }
}


+ [Roll Survival ({DisplayBonus(stat_survival_bonus)}) vs. {DisplayLocation()} (DC {difficulty})]

+ [Edit Survival ({DisplayBonus(stat_survival_bonus)})] -> edit_survival -> find_campsite_check
- 
~ roll = d20()
~ bonus = stat_survival_bonus
~ result = roll + bonus

{result >= difficulty || roll == 20:
    {DisplayD20Result(difficulty)}
    You find a campsite.
- else:
    {DisplayD20Result(difficulty)}
    No proper campsite found. Where did you have to sleep instead?
    You will be exhausted in the morning.
}

- ->->


=== disturbance_while_camping_check ===
-> yes_no_test("How likely are you to be disturbed? Adjust based on population and how much you aced the Survival check.", -> yes, -> no, -> yes)

= yes
You are disturbed by <>

~ roll = d100()
~ bonus = 0

{
    - roll <= 20: a loud noise.
    - roll <= 35: an animal (indifferent): <>
        -> create_creature ->
    - roll <= 50: an animal (hostile): <>
        // Go to the 'creatures' table.
        -> create_creature ->
    - roll <= 60: a disturbing dream/vision.
    - roll <= 80: an NPC (curious):<br><>
        -> create_npc ->
    - roll <= 85: a bandit/thief.
    - roll <= 90: a wandering monster!
        // Roll for 'monster by terrain'.
        -> create_encounter ->
    - roll == 91: a natural distaster.
    - else: storm/weather change.
}

- ->->

= no
You are not disturbed and have a restful night.
->->


