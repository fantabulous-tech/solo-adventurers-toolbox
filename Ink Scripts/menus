
=== main_menu ===

<- menu
<- add_quick_menu(-> main_menu)
<- add_settings_menu(-> main_menu)
-> DONE

= menu
<b>Main Menu</b> ({DisplaySummary()})#CLEAR

~ location = ()

+ [Enter Wilderness]
    ~ terrain = ()
    ~ population = ()
    ~ time = day
    -> wilderness_menu -> main_menu
+ [Enter {dungeon_size:New }Dungeon]
    ~ ResetDungeon()
    -> dungeon_menu -> main_menu
+ {dungeon_size}[Enter Current Dungeon]
    -> dungeon_menu -> main_menu
+ [Enter Town/City]
    ~ urban_size = ()
    ~ time = day
    -> urban_menu -> main_menu

=== location_menu ===
{location == (): -> edit_location ->}
{location == (): ->->}

{location:
    - dungeon: -> dungeon_menu ->
    - urban: -> urban_menu ->
    - wilderness: -> wilderness_menu ->
}

->->


=== quick_menu ===
<- add_quick_menu(-> quick_menu)
<- add_settings_menu(-> quick_menu)
<- add_back_menu(-> back)
-> DONE

- (back)
->->


=== add_settings_menu(-> back) ===
+ [Settings] -> edit_settings -> back

=== add_back_menu(-> back) ===
+ [Back] -> back

=== add_quick_menu(-> back) ===

+ [Create Menu]
    -> create_menu ->

+ [Roll Dice Menu]
    -> roll_dice_menu ->

+ [Yes/No Question]
    -> yes_no_question ->

+ [Skill Check]
    -> skill_check ->

- -> back

=== create_menu ===
{location == ():-> edit_location ->}
{location == ():->->}

{location:
    - urban: -> create_urban_menu ->
    - wilderness: -> create_wilderness_menu ->
    - dungeon: -> create_dungeon_menu ->
    - else:
        ~ logError("Unknown Location {location}.")
}

- ->->

=== add_create_generic_menu(-> back) ===

+ Create NPC
    -> create_npc ->
    ~ again = -> create_npc
    
+ Create Adventurer NPC
    -> create_adventurer ->
    ~ again = -> create_adventurer

+ Create Loot
    {not is_encounter_supported():
        ~logError("Can only create loot for parties of 1 up to level 3 and parties of 2 up to level 2. You are currently a party of {stat_party_size} at level {stat_level}.")
        -> back
    }
    -> create_loot ->
    -> roll_encounter ->
    ~ again = -> create_loot

+ Create Hoard
    {not is_encounter_supported():
        ~logError("Can only create hoards for parties of 1 up to level 3 and parties of 2 up to level 2. You are currently a party of {stat_party_size} at level {stat_level}.")
        -> back
    }
    -> create_hoard ->
    -> roll_encounter ->
    ~ again = -> create_hoard

+ [Change Location]
    -> edit_location -> back

- (sub_menu)
+ [Again] -> again -> sub_menu
+ [Back] -> back



=== create_urban_menu ===
<- menu
<- add_create_generic_menu(-> create_menu)
<- add_settings_menu(-> create_menu)
<- add_back_menu(-> back)
-> DONE

- (back)
->->

= menu
<b>Create Town Stuff Menu</b> #CLEAR
 
+ Create Encounter
    -> create_encounter ->
    ~ again = -> create_encounter
    
+ Create Monster
    -> create_single_monster ->
    ~ again = -> create_single_monster

+ Create Tavern
    -> create_tavern ->
    ~ again = -> create_tavern

- (sub_menu)
+ [Again] -> again -> sub_menu
+ [Back] -> create_urban_menu


=== create_dungeon_menu ===
<- menu
<- add_create_generic_menu(-> create_menu)
<- add_settings_menu(-> create_menu)
<- add_back_menu(-> back)
-> DONE

- (back)
->->

= menu
<b>Create Dungeon Stuff Menu</b> #CLEAR

+ Create Encounter
    -> create_encounter ->
    ~ again = -> create_encounter
    
+ Create Monster
    -> create_single_monster ->
    ~ again = -> create_single_monster

+ Create Room
    -> create_dungeon_room ->
    ~ again = -> create_dungeon_room

+ Create Passage
    -> create_dungeon_passage ->
    ~ again = -> create_dungeon_passage

+ Create Stairs
    -> create_dungeon_stairs ->
    ~ again = -> create_dungeon_stairs

+ Create Door
    -> create_dungeon_door ->
    ~ again = -> create_dungeon_door

+ Create Secret Door
    -> create_secret_door ->
    ~ again = -> create_secret_door

+ Create Trap
    -> create_trap ->
    ~ again = -> create_trap

+ Create Clue
    -> create_dungeon_clue ->
    ~ again = -> create_dungeon_clue

- (sub_menu)
+ [Again] -> again -> sub_menu
+ [Back] -> create_dungeon_menu


=== create_wilderness_menu ===
<- menu
<- add_create_generic_menu(-> create_menu)
<- add_settings_menu(-> create_menu)
<- add_back_menu(-> back)
-> DONE

- (back)
->->

= menu
<b>Create Wilderness Stuff Menu</b> #CLEAR

+ Create Encounter
    -> create_encounter ->
    ~ again = -> create_encounter
    
+ Create Monster
    -> create_single_monster ->
    ~ again = -> create_single_monster

+ Create Clue
    -> create_wilderness_clue ->
    ~ again = -> create_dungeon_clue

+ Create Quest
    -> create_quest ->
    ~ again = -> create_quest

+ Create Creature
    -> create_creature ->
    ~ again = -> create_creature

+ Create Wilderness Encounter
    -> wilderness_encounter_table ->
    ~ again = -> wilderness_encounter_table

+ Create Story Event
    -> story_element_interaction_table ->
    ~ again = -> story_element_interaction_table
    
+ [Terrain Feature Menu]
    -> create_terrain_feature -> menu

- (sub_menu)
+ [Again] -> again -> sub_menu
+ [Back] -> create_wilderness_menu


=== create_terrain_feature ===
<- menu
<- add_create_generic_menu(-> create_terrain_feature)
<- add_settings_menu(-> create_terrain_feature)
<- add_back_menu(-> back)
-> DONE

- (back)
->->

= menu
<b>Create Terrain Feature Menu</b> #CLEAR
    
+ Create Clearfelled Area
    -> create_clearfelled_area ->
    ~ again = -> create_clearing
    
+ Create Clearing
    -> create_clearing ->
    ~ again = -> create_clearing
    
+ Create Gully
    -> create_gully ->
    ~ again = -> create_gully
    
+ Create Hills
    -> create_hills ->
    ~ again = -> create_hills
    
+ Create Landscape Feature
    -> create_landscape_feature ->
    ~ again = -> create_landscape_feature
    
+ Create Landscape Feature
    -> create_lake ->
    ~ again = -> create_lake
    
+ Create Monument
    -> create_monument ->
    ~ again = -> create_monument
    
+ Create Oasis
    -> create_oasis ->
    ~ again = -> create_oasis
    
+ Create Rocky Outcrop
    -> create_outcrop ->
    ~ again = -> create_outcrop
    
+ Create Small Mountain Range / Foothills
    -> create_mountains ->
    ~ again = -> create_mountains
    
+ Create Small Woods
    -> create_woods ->
    ~ again = -> create_mountains

+ Create Structure
    -> create_structure ->
    ~ again = -> create_structure

+ Create Swamp
    -> create_swamp ->
    ~ again = -> create_swamp

+ Create Unmarked Settlement
    -> create_unmarked_settlement ->
    ~ again = -> create_unmarked_settlement

+ Create Waterway
    -> create_waterway ->
    ~ again = -> create_waterway

+ Create Active Area Discovery
    -> create_discovery ->
    ~ again = -> create_discovery

+ [Back] ->->

- (sub_menu)
+ [Again] -> again -> sub_menu
+ [Back] -> create_terrain_feature


