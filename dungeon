

=== dungeon_event ===
-> dungeon_encounter_table ->
->->


=== function ResetDungeon() ===
~ dungeon_room_found = 0
~ dungeon_passages_found = 0
~ dungeon_size = ()
~ dungeon_type = ()
~ dungeon_rooms = 0
~ dungeon_stairs = 0
~ dungeon_traps = 0
~ dungeon_clues = 0
~ dungeon_passages = 0
~ dungeon_doors = 0
~ dungeon_secret_doors = 0
~ dungeon_content_bonus = 0


=== dungeon_menu ===

<b>Entering Dungeon</b> #CLEAR

You can also use the <a href="https:\/\/watabou.itch.io/one-page-dungeon" target="_blank">One Page Dungeon Generator</a>

~ location = dungeon
~ back_menu = -> dungeon_menu

{dungeon_size == (): -> edit_dungeon_size ->}
{dungeon_type == (): -> edit_dungeon_type ->}

{shuffle:
    - ~ dungeon_passages++
    - ~ dungeon_rooms++
}

-> menu


= menu

+ {dungeon_clues}[Look at Dungeon Clue (x{dungeon_clues})]
    ~ dungeon_clues--
    -> create_dungeon_clue -> menu
+ {dungeon_traps}[Look at Trap (x{dungeon_traps})]
    ~ dungeon_traps--
    -> create_trap -> menu
+ {dungeon_doors}[Look at Door (x{dungeon_doors})]
    ~ dungeon_doors--
    -> create_dungeon_door -> menu
+ {dungeon_secret_doors}[Look at Secret Door (x{dungeon_secret_doors})]
    ~ dungeon_secret_doors--
    -> create_secret_door -> menu
+ {dungeon_stairs}[Take Stairs (x{dungeon_stairs})]
    ~ dungeon_stairs--
    -> create_dungeon_stairs -> menu
+ {dungeon_passages}[Take Passage (x{dungeon_passages})]
    ~ dungeon_passages--
    -> create_dungeon_passage -> menu
+ {dungeon_room_found < dungeon_room_total && dungeon_rooms}[Enter Room (x{dungeon_rooms})]
    ~ dungeon_rooms--
    -> create_dungeon_room -> menu

+ Find Place to Camp
    -> camping_menu -> menu

+ Yes/No Question
    -> yes_no_question -> menu

+ Create Other
    -> create_dungeon_menu -> menu

+ [Exit Dungeon]

- ->->



=== create_dungeon_menu ===

+ Create Room
    -> create_dungeon_room ->
    ~ again = -> create_dungeon_room

+ Create Passage
    -> create_dungeon_passage ->
    ~ again = -> create_dungeon_passage

+ Create Stairs
    -> create_dungeon_stairs ->
    ~ again = -> create_dungeon_stairs

+ Create Door
    -> create_dungeon_door ->
    ~ again = -> create_dungeon_door

+ Create Secret Door
    -> create_secret_door ->
    ~ again = -> create_secret_door

+ Create Trap
    -> create_trap ->
    ~ again = -> create_trap

+ Create Clue
    -> create_dungeon_clue ->
    ~ again = -> create_dungeon_clue

+ [Back] -> back_menu

- (sub_menu)
+ [Again] -> again -> sub_menu
+ [Back] -> back_menu


=== create_dungeon_passage ===
~ dungeon_passages_found++
{DisplayDungeonSummary()} - Passage \#{dungeon_passages_found}


Passage Description:<br><>
{shuffle:
    - Passage continues {d4()*10} ft.
        ~ dungeon_passages++
    - Passage goes 15 ft and ends at door.
        ~ dungeon_doors++
    - Passage goes 30 ft and ends in stairs.
        ~ dungeon_stairs++
    - Passage turns left 90 degrees.
        ~ dungeon_passages++
    - Passage turns right 90 degrees.
        ~ dungeon_passages++
    - Passage dead ends. Return to last feature you want to explore.<>
        ~ test = P(40)
        {test:
            Secret door. Notice DC(investigation) d10+7. If found, go to Secret Door choice.
            ~ dungeon_secret_doors++
        }
    - Passage continues {d4()*10} ft and comes to a four way intersection.
        ~ dungeon_passages += 3
    - Passage continues {d4()*10} ft and comes to a T-junction.
        ~ dungeon_passages += 2
    - Passage continues {d6()*10} ft and then you see a side passage leading off to the left.
        ~ dungeon_passages += 2
    - Passage continues {d6()*10} ft and then you see a side passage leading off to the right.
        ~ dungeon_passages += 2
    - Passage ends in an open entrance to a room.
        ~ dungeon_rooms++
    - Door in right wall.
        ~ dungeon_doors++
        ~ dungeon_passages++
    - Door in left wall.
        ~ dungeon_doors++
        ~ dungeon_passages++
    - Secret door on passage wall (player’s choice which wall). Roll perception DC 15. If successful, go to Secret Door. If not, roll again on Passage Table, continuing along (as your PCs didn’t notice.)
        ~ dungeon_secret_doors++
    - Passage narrows {d6()/2*10} ft. (minimum width 5 ft)
        ~ dungeon_passages++
    - Passage widens {d6()/2*10} ft. (minimum width 10 ft)
    - Opening to the left, stairs.
        ~ dungeon_passages++
        ~ dungeon_stairs++
    - Opening to the right, stairs.
        ~ dungeon_passages++
        ~ dungeon_stairs++
    - ~ test = P(50)
        {test:
            ~ dungeon_passages++
        - else:
            ~ dungeon_rooms++
        }
        Opening in the floor, straight drop down {d10()*10} ft to a {test:Passage|Room}.
        ~ dungeon_passages++
    - -> create_architecture ->
        ~ dungeon_passages++
}

~ roll = d100()
~ bonus = dungeon_content_bonus
~ result = roll + bonus

Passage Contents ({DisplayResult()}):<br><>

{
    - result <= 69: Empty
    - result <= 80: Empty apart from rubble. <>
        ~ test = P(10)
        {test:
            Clue. Perception check DC 10 to find.
            ~ dungeon_clues++
        }
    - result <= 84: Empty apart from corpse. <>
        ~ test = P(20)
        {test:
            Clue on body.
            ~ dungeon_clues++
        }
    - result <= 88: Empty apart from multiple corpses. <>
        ~ test = P(40)
        {test:
            Clue on a body.
            ~ dungeon_clues++
        }
    - result <= 90: Empty apart from enemies. 1 level-appropriate easy encounter. Loot 15%, Clue 15%
    - result <= 92: Empty apart from enemies. 1 level-appropriate medium encounter. Loot 25%, Clue 25%
    - result <= 94: Empty apart from enemies. 1 level-appropriate hard encounter. Loot 50%, clue 50%
    - result <= 98: Trap! Use 'Look at Trap'
        ~ dungeon_traps++
    - else: Loot 60%. Roll DMG individual treasure: Challenge 0-4 table. Add 30 to roll. How did this loot get here?
}

~ dungeon_content_bonus = 0

->->

=== create_dungeon_room ===
~ dungeon_room_found++

{DisplayDungeonSummary()} - Room \#{dungeon_room_found}


Room Description:<br><>

~ roll = d20()
~ temp exits = 1

{
    - roll <= 2:
        ~ exits = d6()
        Rectangular, dimensions {d4()*10} ft x {d4()*10} ft. {exits} exits (including the one you entered by)
    - roll <= 4:
        ~ exits = d4()
        Square, {(d4()+1)*10} ft. on all sides, {exits} exits.
    - roll <= 6:
        ~ exits = d6()
        Square {(d6()+1)*10} ft. on all sides, {exits} exits
    - roll <= 8:
        ~ exits = d8()
        Square, {(d8+1)*10} ft. on all sides, {exits} exits
    - roll <= 10:
        ~ exits = d6()
        Rectangle, {(d4()+1)*10} ft x {(d8+1)*10} ft, {exits} exits
    - roll <= 12:
        ~ exits = d6()
        Rectangle, {(d6()+1)*10} ft x {(d6()+2)*10} ft, {exits} exits
    - roll <= 14:
        ~ exits = d4()
        Circular, {d4()*10} ft diameter, {exits} exits
    - roll <= 15:
        ~ exits = d4()
        Triangular, {d6()*10} ft down one side, other sides whatever shape fits best, {exits} exits
    - roll <= 16:
        ~ exits = d4() - 2
        {exits < 1:
            ~ exits = 1
        }
        Pentagonal, {d4()*10} ft. across, {exits} exits
    - roll <= 17:
        ~ exits = d4() - 1
        {exits < 1:
            ~ exits = 1
        }
        Hexagonal, {d6()*10} ft. across, {exits} exits
    - roll <= 18:
        ~ exits = d4() - 1
        {exits < 1:
            ~ exits = 1
        }
        Octagonal, {d6()*10} ft. across, {exits} exits
    - roll <= 19:
        ~ exits = d4() - 2
        {exits < 1:
            ~ exits = 1
        }
        Trapezoidal, {d6()*10} ft roughly on each side.
    - roll <= 20:
        Rough cave, {d12()*10} ft width (approx)
}


~ roll = d100()
~ bonus = dungeon_content_bonus
~ result = roll + bonus

Room Contents: ({DisplayResult()}):<br><>

{
    - result <= 4 : Level appropriate Deadly Encounter (see Chapter 16). Loot 45%, Clue 75%. Roll DMG Loot table appropriate to encounter.
    - result <= 8 : Remnants / proof of the boss or BBEG. It looks as though they have been up to mischief in this room.
    - result <= 12: Low level minions of the BBEG in this room. Level appropriate easy encounter.
    - result <= 20: Dungeon Hazard. 1: sinkhole, 2: fungus, 3: trap, 4: collapsing masonry or other natural hazard, 5: wandering monster, medium difficulty, 6: Player’s choice.
    - result <= 32: Level appropriate Hard Encounter (see Chapter 16). Clue, Loot, Random, SD all 30%.
    - result <= 36: NPC in room investigating. Roll again to find out what they are investigating.
    - result <= 40: A previously triggered trap is in this room, with a level appropriate Hard Encounter enemy caught in it / victim to it. Enemy alive, 30%. Loot 10%.
    - result <= 52: Level appropriate Easy Encounter (see Chapter 16) Loot 20%, SD 10%, Clue 30%
    - result <= 56: Obstacle of some sort (e.g. rubble, crevasse, sinkhole, underground stream, wild magic field). Athletics or acrobatics check might be required to pass it, or it might be impassable. (Alterntively, roll on Obstacles Table, DMG p.297)
    - result <= 67: Level appropriate Medium Encounter (see Chapter 16) Loot 30%, SD 20%, Clue 30%
    - result <= 71: NPC (enemy or ally of the PC) near death (1d4 levels less than you, minimum Level 1), unconscious, Level appropriate Hard Encounter. Enemy or enemies reduced to 50% hp. Loot 50%. A battle has obviously gone on here - the NPC has fought a powerful creature, and lost, but has damaged it somewhat.
    - result <= 74: Two level appropriate ([d4] 1-2: Easy, 3-4: Medium) creatures are engaged in battle with each other. You stumble in just as battle starts. How do you react? Hide and wait until a victor emerges? What are they fighting over? Since they are otherwise engaged, you have advantage on stealth checks to creep up. (DC is the creature’s passive perception)
    - result <= 76: Deserted but for some strange runes and symbols on the floor. Magical? Perhaps.
    - result <= 80: Strong NPC (2d4 levels higher than your PC) has just defeated a Level Appropriate Deadly Encounter. Roll d4 to determine attitude. 1-2: They tell you to begone, they stake claim to this dungeon and all within it. Will fight if challenged. However, also will agree to split zup and going seperate ways, perhaps 3-4: Will team up with you for the remainder of this dungeon, and split the loot, and then will depart. (CR of all encounters from here on increases by 1d4). Random, SD, Trap all 30% Roll on NPC table, skipping professions and going straight to classes. NPC level is determined above.
    - result <= 84: Empty. Mission relevant loot chance 30%.
    - result <= 88: Level appropriate Easy Encounter (see Chapter 16). Clue, NPC, Boon all 30%
    - result <= 92: Accursed or blessed relic, guarded by level appropriate deadly encounter. Q/A rolls to determine nature of relic.
    - else: BOSS / BBEG / Significant NPC Encounter! (Use the details of your story so far, Q/A tables, or Story Element Interaction Tables to determine what). Loot 90%. Roll d20. 1-14: Roll 1d4 times on Individual Treasure Table. 15-20: Roll once on Hoard Table. (DMG pp. 136-139.)
}

~ dungeon_passages += exits - 1
~ dungeon_content_bonus = 0

->->


=== create_dungeon_stairs ===
~roll = d20()

{
    - roll <= 8:
        ~ test = P(50)
        {test:
            ~ dungeon_rooms++
        - else:
            ~ dungeon_passages++
        }
        Down one level to a {test:Room|Passage}.
    - roll <= 9 : Down one level to a Room (+15 to Content roll)
        ~ dungeon_content_bonus = 15
        ~ dungeon_rooms++
    - roll <= 10: Down one level to a Room (+30 to Content roll)
        ~ dungeon_content_bonus = 30
        ~ dungeon_rooms++
    - roll <= 11: Down one level to a Passage
        ~dungeon_passages++
    - roll <= 12: Down one level to a Passage (+15 to Content roll)
        ~ dungeon_content_bonus = 15
        ~dungeon_passages++
    - roll <= 13: Down one level to a Passage (+30 to Content roll)
        ~ dungeon_content_bonus = 30
        ~dungeon_passages++
    - roll <= 14: 
        ~ test = P(50)
        {test:
            ~ dungeon_rooms++
        - else:
            ~ dungeon_passages++
        }
        Down two levels to a {test:Room|Passage}
    - roll <= 15: Up one level to a Room (+15 to Content roll)
        ~ dungeon_content_bonus = 15
        ~ dungeon_rooms++
    - roll <= 16: Up one level to a Room (+30 to Content roll)
        ~ dungeon_content_bonus = 30
        ~ dungeon_rooms++
    - roll <= 17: Up one level to a Passage
    - roll <= 18: Up one level to a Passage (+15 to Content roll)
        ~ dungeon_content_bonus = 15
        ~dungeon_passages++
    - roll <= 19: Up one level to a Passage (+30 to Content roll)
        ~ dungeon_content_bonus = 30
        ~dungeon_passages++
    - roll <= 20:
        ~ test = P(50)
        {test:
            ~ dungeon_rooms++
        - else:
            ~ dungeon_passages++
        }
        Up two levels to a {test:Room|Passage}
}

->->

=== create_secret_door ===
{shuffle:
    - Secret door opens into a room (+40 to Content roll)
        ~ dungeon_content_bonus = 40
        ~ dungeon_rooms++
    - Secret door opens onto passage (+40 to Content roll)
        ~ dungeon_content_bonus = 40
        ~ dungeon_passages++
    - ~ test = P(50)
    Trapped secret door. Roll on trap table, then on {test:Passage|Room} (+50 to Content roll)
        ~ dungeon_content_bonus = 50
        { test:
            ~ dungeon_passages++
        - else:
            ~ dungeon_rooms++
        }
}
->->


=== create_trap ===
~ roll = d100()

{
    - roll <= 6: Poison darts, Notice: 11, Save DC: 10, Damage: (PC level -3) d6 (min 1d6)
    - roll <= 12: Collapsing Roof, Notice: 11, Save DC: 10, Damage: (PC level -2) d6 (min 1d6)
    - roll <= 19: Simple Pit, Notice: 11, Save DC: 11, Damage: (PC level -1) d6 (min 1d6)
    - roll <= 26: Hidden pit, Notice: 11, Save DC: 12, Damage: (PC level -1) d6 (min 1d6)
    - roll <= 32: Locking pit, Notice: 11, Save DC: 12, Damage: (PC level) d6
    - roll <= 38: Spiked pit, Notice: 12, Save DC: 13, Damage: (PC level) d6
    - roll <= 44: Rolling sphere, Notice: 12, Save DC: 14, Damage: (PC level) d6
    - roll <= 50: Scything blade, Notice: 13, Save DC: 14, Damage: (PC level + 1) d6
    - roll <= 56: Glyph trap (1d6: fire / cold / force / lightning) Appropriate attack spell, Notice: 14, Save DC: 15, Damage: (PC level + 1) d6
    - roll <= 63: Magic missile spell, Notice: 14, Save DC: 15, Damage: (PC level + 1) d6
    - roll <= 69: Poison gas released / Acid Spray, Notice: 15, Save DC: 16, Damage: (PC level + 1) d6
    - roll <= 76: Room fills with water, Notice: 15, Save DC: 16, Damage: (PC level + 2) d6
    - roll <= 82: Walls begin closing, Notice: 16, Save DC: 17, Damage: (PC level + 2) d6
    - roll <= 88: Spears come out of floor, Notice: 17, Save DC: 18, Damage: (PC level + 2) d6
    - roll <= 93: Spiked grate drops, Notice: 17, Save DC: 19, Damage: (PC level x 1.5 rounded up) d6
    - roll <= 100: Trapdoor (snakes / acid below?), Notice: 18, Save DC: 20, Damage: (PC level x 2) d6
}
->->


=== create_architecture ===
~ roll = d20()

{shuffle:
    - Statue
    - Series of alcoves
    - Fountain
    - {d4()} Pools
    - Hole in floor
    - Underground terrarium
    - Pillars along each side. Anything hiding there?
    - Catacombs
    - Puzzle of some sort (int check required)
    - Sacrificial plinth
    - Temple / shrine
    - Guard post
    - Trapdoor with ladder
    - Underground stream
    - Natural cavern
    - Ventilation shaft
    - Crystal growth
    - Campsite, abandoned
    - Portal. Takes you to another, random part of the dungeon
    - Edible Fungi / Moss Farm
}

->->

=== create_dungeon_door ===
~ roll = d100()

{
    - roll <= 20:
        ~ dungeon_destination = SetRandomDestination()
        Standard wooden door, braced with metal, unlocked. Opens into {dungeon_destination}.
    - roll <= 25: Iron bars (portcullis) with lever to the side. You can see through to the roome. Pulling the lever will raise the portcullis if unlocked. It is currently {~locked|unlocked}. DC 14 thieves’ tools check to unlock, DC 19 strength check to wrench open. Is the lever trapped? Use Q/A and investigation rolls.
    - roll <= 30:
        ~ dungeon_destination = SetRandomDestination()
        Empty doorway. Perhaps a magic glyph trap, triggering an attack spell (Fire Bolt or other). Make a Q/A roll with the modifier Unlikely (-2) to determine. Beyond doorway is a {dungeon_destination}.
    - roll <= 35:
        Wooden door, locked. DC 15 thieves’ tools check, or it will have to be smashed. (or other valid method, such as Knock spell). Door has AC 12 and 20 hp. Opens into a room.
        ~ dungeon_rooms++
    - roll <= 40:
        ~ dungeon_destination = SetRandomDestinationNoStairs()
        Iron door, locked. DC 14 thieves’ tools check (or other valid method, such as Knock spell, or smashing it down). Opens into {dungeon_destination}.
    - roll <= 45:
        ~ dungeon_destination = SetRandomDestination()
        Locked and trapped stone door. DC 15 perception to find trap. If trap disarmed / avoided / triggered, the door opens into {dungeon_destination}.
        ~ dungeon_traps++
    - roll <= 50:
        ~ test = P(50)
        {test:
            ~ dungeon_passages++
        - else:
            ~ dungeon_rooms++
        }
        Secret door. Does your PC know it’s there? Through to {test:hidden passage|hidden chamber}.
    - roll <= 55:
        Entrance, then 10 ft through to an adjacent passageway. Empty archway, no door.
        ~ dungeon_passages++
    - roll <= 60:
        ~ dungeon_destination = SetRandomDestinationNoStairs()
        Locked stone door, secured with a puzzle. A DC 14 intelligence check will enable the puzzle to be solved and the door unlocked. Opens into {dungeon_destination}.
    - roll <= 75:
        ~ dungeon_destination = SetRandomDestinationNoStairs()
        ~ test = d6() == 1
        {test:
            ~ dungeon_traps++
        }
        {~Locked|Unlocked} {test:trapped|untrapped} {~wooden|stone|iron} door. Opens into a {dungeon_destination}.
    - roll <= 80:
        ~ dungeon_destination = SetRandomDestinationNoStairs()
        Trapped door. DC 15 perception to find trap. If trap disarmed / avoided / triggered, door opens into {dungeon_destination}.
        ~ dungeon_traps++
    - roll <= 85:
        Locked door, can only be opened with a key which is on the body of a humanoid monster somewhere in this dungeon. There is a reason they do not want anyone to enter.
        ~dungeon_rooms++
    - roll <= 90:
        Door composed of elemental energy, such as fire or lightning. You can move through it, but you will take 3d8 damage of whatever type it is composed of.
        ~dungeon_rooms++
    - roll <= 95:
        Heavy stone door, requires athletics check to open (DC 16). -1 hp for every 2 failed strength checks. Opens into room.
        ~dungeon_rooms++
    - else:
        Door is smashed and hanging off its hinges (why?). Leads into a room.
        ~dungeon_rooms++
}
->->

=== roll_dungeon_rooms ===

{dungeon_size == (): -> edit_dungeon_size ->}

{dungeon_size:
    - dng_tiny:
        ~ dungeon_room_total = d4()+2
    - dng_small:
        ~ dungeon_room_total = d6()+4
    - dng_medium:
        ~ dungeon_room_total = d4()+d4()+d4()+6
    - dng_large:
        ~ dungeon_room_total = d6()+d6()+d6()+d6()+d6()+12
    - dng_huge:
        ~ dungeon_room_total = d6()+d6()+d6()+d6()+d6()+d6()+d6()+d6()+d6()+d6()+24
    - dng_limitless:
        ~ dungeon_room_total = (d6()+d6()+d6()+d6()+d6()+d6()+d6()+d6()+d6()+d6()+24)*100
}
->->


=== function SetRandomDestination() ===
~ roll = d4()

{
    - roll <= 1:
        ~ dungeon_passages++
        ~ return Passage
    - roll <= 2:
        ~ dungeon_stairs++
        ~ return Stairs
    - else:
        ~ dungeon_rooms++
        ~ return Room
}

=== function SetRandomDestinationNoStairs() ===
~ roll = d4()

{
    - roll <= 1:
        ~ dungeon_passages++
        ~ return Passage
    - else:
        ~ dungeon_rooms++
        ~ return Room
}


=== edit_dungeon_size ===
{dungeon_size:Dungeon Size: {DisplayDungeonSize()}}
What size is the dungeon?

+ [Random]
    {shuffle:
        - ~ dungeon_size = dng_tiny
        - ~ dungeon_size = dng_small
        - ~ dungeon_size = dng_medium
        - ~ dungeon_size = dng_large
        - ~ dungeon_size = dng_huge
        - ~ dungeon_size = dng_limitless
    }

+ [Tiny (1d4+2 rooms)]
    ~ dungeon_size = dng_tiny
+ [Small (1d6+4 rooms)]
    ~ dungeon_size = dng_small
+ [Medium (4d4+6 rooms)]
    ~ dungeon_size = dng_medium
+ [Large (5d6+23 rooms)]
    ~ dungeon_size = dng_large
+ [Huge (10d6+24 rooms)]
    ~ dungeon_size = dng_huge
+ [Limitless]
    ~ dungeon_size = dng_limitless
+ [Back] ->->

-
-> roll_dungeon_rooms ->
Dungeon Size: {DisplayDungeonSize()}
->->


=== function DisplayDungeonSize() ===
{dungeon_size:
    - (): ~ return "Unknown"
    - dng_tiny: ~ return "Tiny"
    - dng_small: ~ return "Small"
    - dng_medium: ~ return "Medium"
    - dng_large: ~ return "Large"
    - dng_huge: ~ return "Huge"
    - dng_limitless: ~ return "Limitless"
    - else: ~logError("Unknown dungeon size {dungeon_size}.")
}




=== edit_dungeon_type ===
{dungeon_type:Dungeon Type: {DisplayDungeonType()}}
What type of dungeon is this?

+ [Random]
    {shuffle:
        - ~ dungeon_type = dtype_lair
        - ~ dungeon_type = dtype_tomb
        - ~ dungeon_type = dtype_stronghold
        - ~ dungeon_type = dtype_temple
        - ~ dungeon_type = dtype_cave
        - ~ dungeon_type = dtype_maze
        - ~ dungeon_type = dtype_mine
        - ~ dungeon_type = dtype_planar
        - ~ dungeon_type = dtype_hq
        - ~ dungeon_type = dtype_trap
    }

+ [Lair]
    ~ dungeon_type = dtype_lair
+ [Tomb/Crypt]
    ~ dungeon_type = dtype_tomb
+ [Abandoned Stronghold]
    ~ dungeon_type = dtype_stronghold
+ [Temple or Shrine]
    ~ dungeon_type = dtype_temple
+ [Natural Caves]
    ~ dungeon_type = dtype_cave
+ [Maze]
    ~ dungeon_type = dtype_maze
+ [Mine]
    ~ dungeon_type = dtype_mine
+ [Planar Gate]
    ~ dungeon_type = dtype_planar
+ [Guild/Cult Headquarters]
    ~ dungeon_type = dtype_hq
+ [Death Trap]
    ~ dungeon_type = dtype_trap
+ [Back] ->->

-
-> roll_dungeon_rooms ->
Dungeon Type: {DisplayDungeonType()}
->->


=== function DisplayDungeonType() ===
{dungeon_type:
    - (): ~ return "Unknown Type"
    - dtype_lair: ~return "Lair"
    - dtype_tomb: ~return "Tomb/Crypt"
    - dtype_stronghold: ~return "Abandoned Stronghold"
    - dtype_temple: ~return "Temple or Shrine"
    - dtype_cave: ~return "Natural Caves"
    - dtype_maze: ~return "Maze"
    - dtype_mine: ~return "Mine"
    - dtype_planar: ~return "Planar Gate"
    - dtype_hq: ~return "Guild/Cult Headquarters"
    - dtype_trap: ~return "Death Trap"
    - else: ~logError("Unknown dungeon type {dungeon_type}.")
}



=== function DisplayDungeonSummary() ===
~ return "{DisplayDungeonSize()} {DisplayDungeonType()}"


=== dungeon_encounter_table ===
~ roll = d100()
{
    - roll <= 1: You witness a crime and they have a chance to stop the criminal. If you do, you win the favor of the local constabulary. If you try to but fail, the guard gives you an opportunity to help track down the criminal. If you do nothing, you must convince the guard you are not an accomplice.
    - roll <= 5: Something occurs, or you find something, that requires a {~acrobatics/dex check or save|strength or athletics check|stealth check|con check or save|int check|investigation check|arcana check|history check|nature check|religion check}. Use Q/A rolls or Story Element Interaction tables (chapter 14) to find out what.
    - roll <= 10: Something occurs, or you find something, that requires {~an animal handling check|an insight check|a survival check|a medicine check|a perception check|a persuasion check|a performance check|a deception check|an inimidation check|a charisma check}. Use Q/A rolls or Story Element Interaction Tables (chapter 14) to find out what.
    - roll <= 15: -> create_npc ->
        <> gives you information about possible quest. Go to Chapter 7 and generate a quest, which you may or may not accept.

    - roll <= 20: Monster: Level-appropriate easy encounter. Go to Chapter 16.
    - roll <= 25: Monster: Level-appropriate medium encounter. Go to Chapter 16.
    - roll <= 30: Level appropriate hard encounter. Go to Chapter 16.
    - roll <= 35: Level appropriate deadly encounter. Go to Chapter 16. Your PC might want to flee!
    - roll <= 36: Dead beast or monster carcass. What happened here?
    - roll <= 37: You find a strange growth in this area, on the dungeon walls. Is it a moss of some kind?
    - roll <= 38: Part of the floor has crumbled away, revealing a black, empty space below.
    - roll <= 39: A door covered in moss, with a large knocker. Looks like it’s been transplanted here from someone’s garden.
    - roll <= 40: You enter a tomblike area. All of the coffins are child-sized.
    - roll <= 41: Strange vines have grown all through here, creeping around the stonework and gradually, over time, destroying parts of the masonry. How did they get in here?
    - roll <= 42: You see a statue of a wizard in the middle of this area. The plaque beneath reads “I am Fennishaw Eldsight. Speak my name and bring me to life, if you dare!”
    - roll <= 43: You hear voices ahead, and turn the corner to see a barbarian parleying with an ogre! Apparently they are discussing what the ogre’s price is for releasing the half-orc he is currently holding hostage.
    - roll <= 44: A voice repeatedly whispers, “What do you seek?” No matter how you respond, it repeats the same question. (Perhaps until you give the true answer, at which point, you may get what you seek, but not as you expected!) Who does this voice belong to, if anyone?
    - roll <= 45: The ceiling above you opens, and an unexpected NPC falls through screaming, hitting the ground in front of you! Roll NPC Tables to find out who it is.
    - roll <= 46: This area is home to a mining operation. Enslaved humanoid children are toiling away, loading rocks into mine carts for ore processing. Their masters whip and force them to continue on, despite their obvious fatigue.
    - roll <= 47: A middle-aged woman is slumped against the wall, sobbing. She is dressed in rags.
    - roll <= 48: From nowhere, you hear a booming voice: “Take up your weapon, adventurer! You dare to trespass here, now you will face me in battle!” But no one appears. At least not yet… You hear laughing...
    - roll <= 49: Suddenly a vision flickers before you, a vision of your ultimate goal in this dungeon. You do not recognize the room, but get some prior information about what you might be headed towards.
    - roll <= 55: TRAP! Roll on Traps table to find out what.
    - roll <= 56: Graffiti on the wall: “Stop! Bridge out ahead! Go back, your doom is at hand!”
    - roll <= 57: Nearby, you hear a dog barking madly, then another voice telling it to shut up!
    - roll <= 58: You hear screaming, very faint, as if from far, far away.
    - roll <= 59: You round the next corner and see two humanoids engaged in a heated quarrel.
    - roll <= 60: Make a perception check, DC 14. If unsuccessful, you suddenly notice your pack is lighter. Randomly choose one item from your inventory (perhaps figure out a way to randomly roll for it). It has been stolen! You might get it back, but then again...
    - roll <= 61: You come to a door on which a parchment is nailed. “Dungeon caretaker. Enquiries/complaints within.”
    - roll <= 62: Suddenly the wall behind you explodes, and two adventurers enter through it. One is a goliath who has just punched his way through the masonry, the other appears to be a halfling sorcerer.
    - roll <= 63: Ahead, two orc guards sit at a guard post. They appear to be playing dice.
    - roll <= 64: A giant rat runs past you, closely followed by three hungry-looking goblins. They pay you no attention.
    - roll <= 65: As you move through this area, make a perception check, DC 15. If you fail, you do not see a glyph on the wall and trigger an alarm spell! What comes to check?
    - roll <= 66: In a corner, a huge pile of rubbish festers. It is being picked through by some horrid beast or other.
    - roll <= 67: You get the distinct impression that you are being followed. You even fancy you can smell whoever it is, and they smell rank.
    - roll <= 68: You happen upon a massacre. The corpses of a dozen humanoids lie scattered.
    - roll <= 69: A mist swirls about your feet.
    - roll <= 70: You feel a hard shove in your back. When you turn to look, is anyone there?
    - roll <= 71: Roll perception, DC 14. If successful you notice something in your periphery. A stone in the wall shuts with a scrape. Investigation roll, DC 12, to find the mechanism to open it. Beyond is a (d4) 1-2: passage, 3-4: room, containing an encounter (d4) 1: Easy, 2: Medium, 3: Hard, 4: No encounter. Clue 75%, Individual Loot 50%.
    - roll <= 72: An old timer, clearly mad, is perched on a soapbox to the side of this passage, ranting about the current state of political affairs to an audience of one slightly amused boggart.
    - roll <= 73: As you round the next corner, you catch sight of a lone humanoid. Make a stealth check, DC 14. If successful, you stay hidden and watch as they move a stone in the wall, place something behind it, then replace the stone. Then they depart.
    - roll <= 74: You come across a proper dungeon - a locked room with bars, in which you see several ragged-looking prisoners. “Quick, break us out!” one of them exclaims when he sees you.
    - roll <= 75: You come across an ornate door, attended by a spectral figure dressed in opulent clothes. The thing is non-hostile, and manning this door, like a doorman. Roll a d4. On a 1: He praises you and your obvious prowess as an adventurer. He seems to know a lot. The spectral doorman admits opens the door for you. On a 2: He begins insulting you, pointing out how awful your clothes are, how ugly you are, how terrible you smell! Make a wisdom check, DC 15. If you succeed, he congratulates you on your poise in the face of abuse and lets you through the door. On 3-4: He begins debating with you as to why you shouldn’t be admitted through the door. Make an intelligence check or a persuasion check, DC 14. If successful, you win the debate and he lets you through the door. The spectral doorkeeper cannot be attacked or moved. And the door only opens for him. (40% boss encounter).
    - roll <= 76: A kindly little goblin appears with hat in hand, and begs your help in ridding him of a problem. He has just returned from a trip to the nearest town to find that his home has been taken over! Two levels down, his humble little abode that he shares with his wife and two little goblings, has been invaded by a horrible (insert level appropriate hard encounter here). If he could get your assistance in getting rid of this scourge, he will reward you with all his profits from his recent sales trip - 250 gp! And his wife will cook you a delicious pot of Tunnelslug Stew, finest in the dungeon, they say!
    - roll <= 77: You run into some rival adventurers, who happen to be there following the same leads as you. Will you team up with these NPCs? (Use NPC tables to determine who they are, skipping Profession and going straight to Class Tables).
    - roll <= 78: You take one more step and… boom! You are in an utterly unfamiliar part of the dungeon. Abandon the map you have so far. You have stepped through a portal, and now are not even sure you are in the same dungeon, realm, or world! Where have you been transported?
    - roll <= 79: You find a secret entrance to an opulent room, lined with bookshelves. A fire crackles in the hearth, and richly-patterned rugs and furniture decorate this cosy space. Who lives here?
    - roll <= 80: You find small circular openings in the walls. Each contains a scroll. Withdrawing them, you see that they bear writing from some ancient civilization. These could be of great worth to an archaeologist, but first you will need to get them translated. What secrets do they hold? (Roll a d8. The scrolls are in 1: Infernal, 2: Abyssal, 3: Celestial, 4: Draconic, 5: Druidic, 6: Giant, 7: Primordial, 8: Undercommon. If you happen to speak the resultant language, use Q/A rolls to determine what they contain. Perhaps a quest! Or some valuable knowledge. They might be worth something, too, to the right buyer).
    - roll <= 81: 1d4-1 humanoids are arguing and do not notice your approach. One is a commander, the other(s) subordinate. The subordinate has failed in the execution of some task. The rest of your story will hopefully provide the context here. If you choose to attack, this will be a (d4) 1: Easy, 2-3: Medium, 4: Hard encounter. You may choose an appropriate enemy based on the encounter guidelines in Chapter 16. Individual loot 50% (roll twice, once for each humanoid)
    - roll <= 82: You find the shrine of a long-forgotten deity. (Loot Hoard 30%). Perhaps this deity tries to commune with you, attempting to find a new worshipper?
    - roll <= 83: You see an ornate historical mural on a nearby wall, faded but obviously painted / carved by a master artist. As you watch, the figures in the mural come to life and begin to play out a scene hitherto unknown to you. You gain knowledge of this episode of history, and possibly of some previously undiscovered location/quest/relic associated with it. (Consult the history of your setting / use Q/A rolls to find out what… player’s choice!)
    - roll <= 84: You enter a chamber where a sacrificial ceremony is taking place. Several humanoids are gathered around a plinth, about to sacrifice a live beast. Who knows what the outcome of this ritual will be?
    - roll <= 85: You get the feeling you are being stalked by something, some invisible force. You see shadows, hear noises, feel a presence, but every time you turn you see nothing. Perhaps it will show itself eventually. Perhaps it is another adventurer, or perhaps… something more sinister.
    - roll <= 86: In the next room, a gnome sits at a table in a sparse room before a chess board. He invites you to play. Playing requires a DC 15 deception or intelligence check. If successful, the gnome will gift you with a useful trinket of gnomish invention, or
    - roll <= 50: gp. If unsuccessful, you surrender an item of 50 gp worth. You can refuse this
    - roll <= 34: invitation if you wish. The gnome will also just talk if you want - he will welcome the company, and is a wealth of information about the dungeon.
    - roll <= 87: You find an underground tavern, where dungeon denizens like to stop and get an ale every now and then before returning to their murderous exploits! It’s called The Delver’s Rest and the patronage is steady! (If this is too silly, re-roll).
    - roll <= 88: You find a secret door to a chamber where an alchemist is busy preparing potions and tinctures of all kinds. He might be open to bartering, but is currently trying to prepare something that will “stop all hell breaking out in this place.” If you try to attack him, you find he is protected by an impenetrable magic barrier, which only he can let down.
    - roll <= 89: You find a dimensional door into a forest, but can’t be sure if this wilderness is real, or simply an illusion to lead you into even greater danger.
    - roll <= 90: In this area are some pools of greyish-green slime of unknown nature. Q/A and nature rolls to determine more about their nature.
    - roll <= 91: If the party are holding torches, or there are torches or candles present in the area, something blows them all out suddenly.
    - roll <= 92: The area is filled with a thick mist/fog, that hangs heavy and low to the ground.
    - roll <= 93: In the distance a small girl dressed in rags can be seen standing near the wall. If players move toward her, she will turn and run through the nearest wall and disappear.
    - roll <= 94: To the far side three skeletons hang on manacles, if they are inspected 1d6 rats will jump out at the PCs.
    - roll <= 95: The ground here is earthen and covered with a top soil. It smells heavily of must and dirt. Root-like tendrils will start to appear out of the ground and start to grow up the walls.
    - roll <= 96: A player is bitten on the hand (causing minimal if no damage). What did it?
    - roll <= 97: A low rumble, then a deep cracking noise is heard directly above. The area shakes slightly, dust and small debris falls from the ceiling, then it stops.
    - roll <= 98: A bard has stopped in this area to belt out a quick song to try and lift his own spirits. He’s been lost down here for days! He tells you that he’s managed to find his way up to this level with some difficulty, but that this dungeon descends many levels downward. He was held captive by some kind of humanoid until just recently. He’s absolutely starving, having had to subsist on dungeon moss and tunnelslugs for weeks.
    - roll <= 99: You hear the sound of something heavy being dragged, coming from up ahead.
    - roll <= 100: A large group of humanoids comes running down the tunnel towards you, their expressions showing absolute, abject terror. “RUN!” the lead one yells at you. “Save yourself, run, now!!!” Behind them, you see something BIG following...
}

->->


